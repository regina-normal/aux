# Maintainer: Ben Burton <bab@debian.org>
pkgname=regina-normal
pkgver=6.0.1
pkgrel=1
pkgdesc="Mathematical software for low-dimensional topology"
arch=('x86_64')
url="https://regina-normal.github.io/"
license=('GPL')
# The Arch Linux packaging guidelines say *not* to rely on transitive
# dependencies.  So, although namcap complains about them, we include
# them here nevertheless.  They include:
#   - gmp (<- coreutils <- base)
#   - libxml2 (<- shared-mime-info)
#   - perl (<- netpbm <- gts <- graphviz)
#   - popt (<- cryptsetup <- systemd <- base)
#   - qt5-base (<- qt5-svg)
#   - shared-mime-info (<- qt5-base)
#   - zlib (<- python)
depends=(gmp
         graphviz
         jansson
         hicolor-icon-theme
         libxml2
         perl
         popt
         python
         qt5-base
         qt5-svg
         shared-mime-info
         tokyocabinet
         zlib)
makedepends=(boost
             cmake
             cppunit
             doxygen
             libxslt)
checkdepends=(cppunit)
source=("https://github.com/regina-normal/regina/releases/download/regina-$pkgver/regina-$pkgver.tar.gz")
sha256sums=('afdba96812bb0187db10b6f0285e4657809d31fda23868f984663f06edc1537a')

build() {
    cd "$srcdir/regina-$pkgver"
    rm -rf build
    mkdir build
    cd build
    cmake -DPACKAGE_NAME=regina-normal \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DPython_EXECUTABLE=/usr/bin/python3 \
          -DPACKAGING_MODE=1 \
          -DDISABLE_RPATH=1 \
          ..
    make
}

check() {
    cd "$srcdir/regina-$pkgver/build"
    make test ARGS=-V
}

package() {
    cd "$srcdir/regina-$pkgver/build"
    make DESTDIR="$pkgdir/" install
}
