# Maintainer: Ben Burton <bab@debian.org>
pkgname=tokyocabinet
pkgver=1.4.48
pkgrel=1
pkgdesc="An efficient database library like GDBM and NDBM"
arch=('x86_64')
url="http://fallabs.com/tokyocabinet/"
license=('LGPL')
# We do not list any makedepends: we need nothing beyond what is already
# provided by base-devel, and we may assume that is already installed.
depends=(zlib bzip2)
source=("http://fallabs.com/tokyocabinet/${pkgname}-${pkgver}.tar.gz"
         tokyocabinet-rpath.patch)
sha256sums=('a003f47c39a91e22d76bc4fe68b9b3de0f38851b160bbb1ca07a4f6441de1f90'
            '7d86d6403e9661279a2109142fffa19b696e8342dbbe3f10c6d7667bfa21fd92')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Remove the insecure RPATH $HOME/lib, plus related occurrences of $HOME.
  # This patch is taken from the debian packages, and incorporates:
  # - 0002-Avoid-LDENV-to-avoid-rpaths.patch
  # - 0007-fix-FTBFS-with-binutils-gold.patch
  patch --forward -p1 < "$srcdir/tokyocabinet-rpath.patch"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --libexecdir="/usr/lib/$pkgname" --enable-off64 --enable-fastest
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make -k check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}

