# Maintainer: Ben Burton <bab@debian.org>
pkgname=regina-normal
pkgver=7.4.1
pkgrel=1
pkgdesc="Mathematical software for low-dimensional topology"
arch=('x86_64')
url="https://regina-normal.github.io/"
license=('GPL-2.0-or-later')
# The Arch Linux packaging guidelines say *not* to rely on transitive
# dependencies.  So, although namcap complains about them, we include
# them here nevertheless.  They include:
#   - gmp (<- coreutils <- base)
#   - libxml2 (<- shared-mime-info)
#   - perl (<- netpbm <- gts <- graphviz)
#   - qt6-base (<- qt6-svg)
#   - shared-mime-info (<- qt6-base)
#   - zlib (<- python)
depends=(gmp
         graphviz
         hicolor-icon-theme
         libxml2
         lmdb
         perl
         python
         qt6-base
         qt6-svg
         shared-mime-info
         zlib)
makedepends=(cmake
             doxygen
             libxslt)
source=("regina-$pkgver.tar.gz")
sha256sums=('74131014099b77083dd1d09004fd6bc972421c792d188cdf43f0fd11ca6cb2ed')

build() {
    cd "$srcdir/regina-$pkgver"
    rm -rf build
    mkdir build
    cd build
    cmake -DPACKAGE_NAME=regina-normal \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBEXECDIR=lib \
          -DPython_EXECUTABLE=/usr/bin/python3 \
          -DPACKAGING_MODE=1 \
          -DDISABLE_RPATH=1 \
          -DBUILD_INFO="Upstream Arch Linux package" \
          ..
    make
}

check() {
    cd "$srcdir/regina-$pkgver/build"
    make test ARGS=-V
}

package() {
    cd "$srcdir/regina-$pkgver/build"
    make DESTDIR="$pkgdir/" install
}
