# Maintainer: Ben Burton <bab@debian.org>
pkgname=regina-normal
pkgver=6.1
pkgrel=1
pkgdesc="Mathematical software for low-dimensional topology"
arch=('x86_64')
url="https://regina-normal.github.io/"
license=('GPL')
depends=(graphviz
         jansson
         hicolor-icon-theme
         lmdb
         python
         qt5-svg)
# There are other dependencies, but namcap complains that they are redundant:
#   - base brings in popt (via systemd and cryptsetup), gmp (via coreutils)
#   - graphviz brings in perl (via gts and netpbm)
#   - python brings in zlib
#   - qt5-svg brings in qt5-base, shared-mime-info
#   - shared-mime-info brings in libxml2
makedepends=(cmake
             cppunit
             doxygen
             libxslt)
checkdepends=(cppunit)
source=("regina-$pkgver.tar.gz")
sha256sums=(SKIP)

build() {
    cd "regina-$pkgver"
    rm -rf build
    mkdir build
    cd build
    cmake -DPACKAGE_NAME=regina-normal \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DPython_EXECUTABLE=/usr/bin/python3 \
          -DPACKAGING_MODE=1 \
          -DDISABLE_RPATH=1 \
          ..
    make
}

check() {
    cd "regina-$pkgver/build"
    make test ARGS=-V
}

package() {
    cd "regina-$pkgver/build"
    make DESTDIR="$pkgdir/" install
}
