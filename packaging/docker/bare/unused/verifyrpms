#!/usr/bin/perl -w
use File::Basename;
use File::Temp;
use strict;

my $rpmkeys = '/usr/bin/rpmkeys';

sub usage {
  my $reason = shift;
  defined $reason and print STDERR "ERROR: $reason\n\n";
  print STDERR "Usage: $0 <keyfile> [rpm]...\n";
  exit 1;
}

my $keyfile = shift;
defined $keyfile or usage('No key file was supplied.');
-e $keyfile or usage("The key file $keyfile does not exist.");

my $rpmdb = File::Temp->newdir(TMPDIR => 1, TEMPLATE => 'rpmdbXXXX');
my $rpmdirname = $rpmdb->dirname;

# print("Temporary RPM database directory: $rpmdirname\n");
print("Using keys from $keyfile...\n");
my @args = ($rpmkeys, "--dbpath=$rpmdirname", '--import', $keyfile);
system(@args) and die;

my $exitcode = 0;
my $count = 0;

defined $ARGV[0] or usage('No RPMs were supplied.');
foreach my $rpm (@ARGV) {
  -e $rpm or usage("The RPM $rpm does not exist.");

  $count = $count + 1;
  my $basename = basename $rpm;
  # print("Verifying $basename...\n");
  my $output = `$rpmkeys --dbpath='$rpmdirname' --checksig '$rpm'`;
  chomp $output;
  $output eq "$rpm: digests signatures OK" and next;

  $output =~ s/^\/[^:]*\/([^:\/]*): /ERROR: $basename: /;
  print("$output\n");
  $exitcode = 1;
}

$exitcode == 0 and print("All $count RPMs successfully verified.\n");
exit $exitcode;
